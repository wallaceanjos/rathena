//===== Cronus Script ========================================
//= Habilidades Platina
//===== Por: =================================================
//= Mehah
//===== Versão Atual: ========================================
//= 1.3a
//===== Descrição: ===========================================
//= Recebe as habilidade de Quest
//===== Changelog: ===========================================
//= 1.1 Adicionado suporte as novas habilidades de Quest [Mehah]
//= 1.2 Removido Fingir de Morto das Classes Extendeds [Pudim]
//= 1.2a Pequenas correções e mudanças. [RoM]
//= 1.3 Removida a variável do npc_athena.conf (ele não existe mais). [RoM]
//= 1.3a Removido show [Keoy]
//============================================================

prontera,128,200,6	script	Habilidades de Platina	94,{

	// Custo para receber as habilidades:
	set .@custo,0;

	mes "[Habilidades de Platina]";
	mes "Olá, você deseja receber suas habilidades extras?";
	if (.@custo)
		mes "Irá custar "+.@custo+" Zeny.";
	next;
	if (select("Sim, por favor:Não") == 2) {
		mes "[Habilidades de Platina]";
		mes "Tenha um bom dia...";
		close;
	}
	
	if (.@custo > Zeny) {
		mes "[Habilidades de Platina]";
		mes "Você não possui toda quantia de Zeny.";
		close;
	}
	
	set Zeny, Zeny - .@custo;

	skill 142,1,0;
	switch(BaseClass) {
		case 0: if (Class !=23) skill 143,1,0; break;
		case 1: skill 144,1,0; skill 145,1,0; skill 146,1,0; break;
		case 2: skill 157,1,0; break;
		case 3: skill 147,1,0; skill 148,1,0; break;
		case 4: skill 156,1,0; break;
		case 5: skill 153,1,0; skill 154,1,0; skill 155,1,0; break;
		case 6: skill 149,1,0; skill 150,1,0; skill 151,1,0; skill 152,1,0; break;
		default: break;
	}
	switch(roclass(eaclass()&EAJ_UPPERMASK)) {
		case 7: skill 1001,1,0; break;
		case 8: skill 1014,1,0; break;
		case 9: skill 1006,1,0; break;
		case 10: skill 1012,1,0; skill 1013,1,0; break;
		case 11: skill 1009,1,0; break;
		case 12: skill 1003,1,0; skill 1004,1,0; break;
		case 14: skill 1002,1,0; break;
		case 15: skill 1015,1,0; skill 1016,1,0; break;
		case 16: skill 1007,1,0; skill 1008,1,0; skill 1017,1,0; skill 1018,1,0; skill 1019,1,0; break;
		case 17: skill 1005,1,0; break;
		case 18: skill 238,1,0; break;
		case 19: skill 1010,1,0; break;
		case 20: skill 1011,1,0; break;
		default: break;
	}

	mes "[Habilidades de Platina]";
	mes "Prontinho!";
	close;
}
