//===== rAthena Script =======================================
//= Stylist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Changes your hair style, hair color, and cloth color.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//============================================================

prontera,170,180,1	script	Estilista	122,{
	mes "[^FF0000Estilista^000000]";
	mes "Bem-vind"+ (Sex?"o":"a") +" "+strcharinfo(0)+",";
	mes "o que voce gostaria de alterar?";
	next;
	if(select("Estilo do Cabelo","Cor do Cabelo","Cor da Roupa","Cancelar") == 4) close;
	if(@menu == 1) setarray .@cor[0],1,getbattleflag ("min_hair_style"),getbattleflag ("max_hair_style");
	if(@menu == 2) setarray .@cor[0],6,getbattleflag ("min_hair_color"),getbattleflag ("max_hair_color");
	if(@menu == 3) setarray .@cor[0],7,getbattleflag ("min_cloth_color"),getbattleflag ("max_cloth_color");
	while(1) {
		if(select("Proximo","Anterior","Digitar","Cancelar") == 4) close;
		if(@menu == 1)
			if(getlook(.@cor[0]) >= .@cor[2]) setlook .@cor[0],.@cor[1];
			else setlook .@cor[0],getlook(.@cor[0])+1;
		if(@menu == 2)
			if(getlook(.@cor[0]) <= .@cor[1]) setlook .@cor[0],.@cor[2];
			else setlook .@cor[0],getlook(.@cor[0])-1;
		if(@menu == 3) {
			input .@num,.@cor[1],.@cor[2];
			setlook .@cor[0],.@num;
		}
		mes "Numero ^FF0000"+getlook(.@cor[0])+"^000000/"+.@cor[2];
		specialeffect2 339;		
	}
}
